apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'groovy'
apply from: file("etc/emma.gradle")

group = 'io.qdb'
version = '0.1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        url "/Users/david/.m2/repository"
    }
}

dependencies {
    compile 'io.qdb:qdb-buffer:0.3.0-SNAPSHOT'
    compile 'ch.qos.logback:logback-core:1.0.7'
    compile 'ch.qos.logback:logback-classic:1.0.7'
    compile 'org.slf4j:slf4j-api:1.6.6'

    testCompile 'com.fasterxml.jackson.core:jackson-databind:2.1.2'
    testCompile "org.spockframework:spock-core:0.6-groovy-1.8"
    testCompile 'junit:junit:4.10'
    testCompile 'commons-io:commons-io:2.4'
    groovy "org.codehaus.groovy:groovy-all:1.8.6"
}

task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn:javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourceJar
    archives javadocJar
}

signing {
    sign configurations.archives
}

def sonatypeUsername = properties.sonatypeUsername
def sonatypePassword = properties.sonatypePassword

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { deployment -> signing.signPom(deployment) }
            if (version.endsWith("SNAPSHOT")) {
                repository(url: "https://oss.sonatype.org/content/repositories/snapshots") {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }
            } else {
                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }
            }
            pom.project {
                name 'qdb-kvstore'
                packaging 'jar'
                description 'Transactional in memory key/value store with disk based transaction log and clustering'
                url 'http://qdb.io/qdb-kvstore'
                scm {
                    url 'scm:git@github.com:qdb-io/qdb-kvstore.git'
                }
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }
                developers {
                    developer {
                        id 'davidtinker'
                        name 'David Tinker'
                    }
                }
            }
        }
    }
}
